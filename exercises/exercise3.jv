// Author: Omer Khan Jadoon

pipeline GoodsPipeline {

    GoodsHttpExtractor
        -> GoodsTextFileInterpreter
        -> GoodsCSVInterpreter
        -> MetadataRemover
            
        -> HeaderWriter
        -> QuantityHeaderWriter

        -> FooterRemover
      
        -> GoodsTableInterpreter
        -> GoodsLoader;

    valuetype GoodsId oftype text {
        constraints: [ GoodsIdFormat ];
    }

    constraint GoodsIdFormat oftype RegexConstraint {
        regex: /^NST7-[0-9A-Z]{3}$/;
    }

    valuetype Month oftype text {
        constraints: [ MonthFormat ];
    }

    constraint MonthFormat oftype AllowlistConstraint {
        allowlist: [
            "Januar", "Februar", "MÃ¤rz", "April", "Mai", "Juni",
            "Juli", "August", "September", "Oktober", "November", "Dezember"
        ];
    }

    block GoodsHttpExtractor oftype HttpExtractor {
        url: "https://www-genesis.destatis.de/genesis/downloads/00/tables/46131-0014_00.csv";
    } 

     block GoodsTextFileInterpreter oftype TextFileInterpreter {
        encoding: "latin2";   // Ensure correct encoding for German umlauts
    }

    block GoodsCSVInterpreter oftype CSVInterpreter {
        delimiter: ';';
    }

    block MetadataRemover oftype RowDeleter {
        delete: [row 1, row 2, row 3, row 4, row 5, row 6, row 7];  // Skip the first 7 metadata rows
    }

    block FooterRemover oftype RowDeleter {
        delete: [row 41326, row 41327, row 41328, row 41329, row 41330];  // Skip the last 5 metadata rows
    }

    block HeaderWriter oftype CellWriter {
        at: range A1:E1;
        write: ["year", "month", "goods_id", "goods_name", "goods_source"];
    }

    block QuantityHeaderWriter oftype CellWriter {
        at: range AT1:AU1;
        write: ["abroad", "total"];
    }

    block GoodsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "year" oftype integer,
            "month" oftype Month,
            "goods_id" oftype GoodsId,
            "goods_name" oftype text,
            "goods_source" oftype text,
            "abroad" oftype integer,
            "total" oftype integer
        ];
    }

    block GoodsLoader oftype SQLiteLoader {
        table: "goods";
        file: "./goodsTransportedByTrain.sqlite";
    }

}


