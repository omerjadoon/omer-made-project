composite blocktype TextFileExtractor {
    property url oftype text;

    input inputName oftype None;
    output outputName oftype File;

    block FileExtractor oftype HttpExtractor { url: url; }

    block FileTextInterpreter oftype TextFileInterpreter {}

    inputName
        ->FileExtractor
        ->FileTextInterpreter
        ->outputName;
}

composite blocktype CSVExtractor {
    property url oftype text;
    property delimiter oftype text: ',';
    property enclosing oftype text: '';
    property enclosingEscape oftype text: '';

    input inputName oftype None;
    output outputName oftype Sheet;

    block TextFileExtractor oftype TextFileExtractor { url: url; }

    block FileCSVInterpreter oftype CSVInterpreter {
        delimiter: delimiter;
        enclosing: enclosing;
        enclosingEscape: enclosingEscape;
    }

    inputName
        ->TextFileExtractor
        ->FileCSVInterpreter
        ->outputName;
}

composite blocktype GoodsTransportedTableInterpreter {
    input inputName oftype Sheet;
    output outputName oftype Table;

    block GoodsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "year" oftype integer,
            "month" oftype text,
            "goods_id" oftype text,
            "goods_name" oftype text,
            "goods_source" oftype text,
            "abroad" oftype integer,
            "total" oftype integer
        ];
    }

    inputName->GoodsTableInterpreter->outputName;
}

composite blocktype GoodsSink {
    input inputName oftype Table;
    output outputName oftype None;

    block GoodsLoader oftype SQLiteLoader {
        table: "goods";
        file: "./goodsTransportedByTrain.sqlite";
    }

    inputName->GoodsLoader->outputName;
}

pipeline GoodsPipeline {
    GoodsCSVExtractor 
        -> GoodsTableProcessor
        -> GoodsLoader;

    block GoodsCSVExtractor oftype CSVExtractor {
        url: "https://www-genesis.destatis.de/genesis/downloads/00/tables/46131-0014_00.csv";
        enclosing: '"';
    }

    block GoodsTableProcessor oftype GoodsTransportedTableInterpreter {
    }

    block GoodsLoader oftype GoodsSink {
    }
}